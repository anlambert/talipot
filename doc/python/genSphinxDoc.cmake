# Turn back the CMAKE_LIBRARY_PATH string into a list
STRING(REPLACE "," ";" CMAKE_LIBRARY_PATH "${CMAKE_LIBRARY_PATH}")

# On windows, we need to add the paths to all the dll dependencies of the Talipot Python modules
# in the PATH environment variable, otherwise the import of the talipot modules will fail
IF(WIN32)
  SET(DEPENDENCIES_PATHS "${BIN_DIR}/../../library/talipot-core/src"
                         "${BIN_DIR}/../../library/talipot-gui/src"
                         "${BIN_DIR}/../../library/talipot-ogl/src"
                         "${BIN_DIR}/../../library/talipot-ogdf/src/"
                         "${BIN_DIR}/../../library/talipot-python/src/"
                         "${BIN_DIR}/../../thirdparty/gzstream"
                         "${BIN_DIR}/../../thirdparty/quazip"
                         "${BIN_DIR}/../../thirdparty/ftgl"
                         "${BIN_DIR}/../../thirdparty/OGDF"
                         "${BIN_DIR}/../../thirdparty/yajl/src"
                         "${BIN_DIR}/../../thirdparty/libtess2"
                         "${CMAKE_LIBRARY_PATH}"
                         "${QT_BIN_DIR}")

  SET(ENV{PATH} "${DEPENDENCIES_PATHS};$ENV{PATH}")
  SET(ENV{QT_QPA_PLATFORM_PLUGIN_PATH} "${QT_BIN_DIR}/../plugins/platforms")

ENDIF(WIN32)

# Add the paths of the talipot Python modules in the PYTHONPATH environment variable
# in order for the interpreter to import them
SET(PYTHON_PATHS "${BIN_DIR}/../../library/talipot-python/bindings/talipot-core/talipot_module"
                 "${BIN_DIR}/../../library/talipot-python/bindings/talipot-gui/talipotgui_module"
                 "${BIN_DIR}/../../library/talipot-python/modules"
                 "${SRC_DIR}")

# Add the path of the sip Python module if we compile it from thirdparty as
# the talipot modules depend on it
IF(NOT SYSTEM_SIP)
  SET(PYTHON_PATHS "${BIN_DIR}/../../thirdparty/sip-${SIP_VERSION}/siplib"
                   "${PYTHON_PATHS}")

ENDIF(NOT SYSTEM_SIP)

# On non windows systems, the character separating the paths in an environment variable
# is a ':' not a ';' (generated by CMake when turning a list into string)
IF(NOT WIN32)
  STRING(REPLACE ";" ":" PYTHON_PATHS "${PYTHON_PATHS}")
ENDIF(NOT WIN32)

SET(ENV{PYTHONPATH} "${PYTHON_PATHS}")

SET(ENV{TALIPOT_BUILD_DIR} "${TALIPOT_BUILD_DIR}")
SET(ENV{TALIPOT_SOURCE_DIR} "${TALIPOT_SOURCE_DIR}")
SET(ENV{TALIPOT_PYTHON_PLUGINS_DIR} "${BIN_DIR}/../../library/talipot-python/bindings/talipot-core/talipot_module/talipot/plugins")
SET(ENV{TalipotGUI_PYTHON_PLUGINS_DIR} "${BIN_DIR}/../../library/talipot-python/bindings/talipot-gui/talipotgui_module/talipotgui/plugins")

# Fix an encoding issue on Mac OS
IF(APPLE)
  SET(ENV{LC_ALL} "en_EN.UTF-8")
ENDIF(APPLE)

# Generate Talipot plugins documentation file from embedded plugins metadata
EXECUTE_PROCESS(COMMAND ${PYTHON_EXECUTABLE} ${BIN_DIR}/gen_plugins_doc.py WORKING_DIRECTORY ${BIN_DIR})

# Everything is now set up, we can generate the documentation
EXECUTE_PROCESS(COMMAND ${SPHINX_EXECUTABLE} -c ${BIN_DIR} -b html -E -d ${BIN_DIR}/doctrees ${SRC_DIR} ${BIN_DIR}/html)
