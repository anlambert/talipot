!define PYTHON_INSTALLER_EXT "@PYTHON_INSTALLER_EXT@"

!include "MUI.nsh"
!include "x64.nsh"
!include "FileAssociation.nsh"

Function .onInit
  ${If} ${RunningX64}
   Goto Yes
  ${Else}
    MessageBox MB_OK "You cannot install the 64 bit version of Talipot @TalipotVersion@ on a Windows 32 bit system. Use the 32 bit version of Talipot @TalipotVersion@ instead."
    Abort
  ${Endif}
Yes:
FunctionEnd
  InstallDir "$PROGRAMFILES64\Talipot-@TalipotVersion@"
  Name "Talipot-@TalipotVersion@ (x64)"

!define MUI_ICON "files\share\talipot\bitmaps\talipot.ico"
!define MUI_UNICON "files\share\talipot\bitmaps\talipot.ico"

RequestExecutionLevel admin

!define MUI_ABORTWARNING

; Installer pages
!insertmacro MUI_PAGE_LICENSE "LICENSE"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

; Uninstaller pages
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

; Language
!insertmacro MUI_LANGUAGE "English"

; Setup file
!ifndef OUT_FILE
!define OUT_FILE "@CMAKE_BINARY_DIR@/Talipot-@TalipotVersion@_x64_python@PYTHON_VERSION_NO_DOT@@SUFFIX@_setup.exe"
!endif
OutFile "${OUT_FILE}"

Function CheckAndInstallPython
  SetRegView 64
  ReadRegStr $0 HKCU SOFTWARE\Python\PythonCore\@PYTHON_VERSION@\InstallPath ""
  IfErrors +2 0
  IfFileExists $0\python.exe end 0
  ReadRegStr $0 HKLM SOFTWARE\Python\PythonCore\@PYTHON_VERSION@\InstallPath ""
  IfErrors install 0
  IfFileExists $0\python.exe end install
  install:
        MessageBox MB_OK "Your system does not appear to have Python @PYTHON_VERSION@ (x86-64) installed.$\n$\nIt is now required to run Talipot @TalipotVersion@.$\n$\nPress OK to download https://www.python.org/ftp/python/@PYTHON_VERSION_WITH_PATCH@/@PYTHON_INSTALLER@$\n$\nand install Python @PYTHON_VERSION@ (x86-64)."
        inetc::get "https://www.python.org/ftp/python/@PYTHON_VERSION_WITH_PATCH@/@PYTHON_INSTALLER@" "python-@PYTHON_VERSION_WITH_PATCH@.@PYTHON_INSTALLER_EXT@"
!if ${PYTHON_INSTALLER_EXT} == "msi"
        ExecWait '"msiexec" /i "python-@PYTHON_VERSION_WITH_PATCH@.@PYTHON_INSTALLER_EXT@"' $0
!else
	ExecWait '"python-@PYTHON_VERSION_WITH_PATCH@.@PYTHON_INSTALLER_EXT@"' $0
!endif
        Delete python-@PYTHON_VERSION_WITH_PATCH@.@PYTHON_INSTALLER_EXT@
		${If} $0 != 0
		   DetailPrint "MSI return code was $0"
		   MessageBox MB_OK "Python @PYTHON_VERSION@ (x86-64) installation failed. Talipot @TalipotVersion@ installation will proceed$\n$\nbut Talipot will not work, until Python @PYTHON_VERSION@ (x86-64) is installed."
		${EndIf}
  end:
FunctionEnd

!define SHCNE_ASSOCCHANGED 0x08000000
!define SHCNF_IDLIST 0

Function RefreshShellIcons
  ; By jerome tremblay - april 2003
  System::Call 'shell32.dll::SHChangeNotify(i, i, i, i) v \
  (${SHCNE_ASSOCCHANGED}, ${SHCNF_IDLIST}, 0, 0)'
FunctionEnd

Function un.RefreshShellIcons
  ; By jerome tremblay - april 2003
  System::Call 'shell32.dll::SHChangeNotify(i, i, i, i) v \
  (${SHCNE_ASSOCCHANGED}, ${SHCNF_IDLIST}, 0, 0)'
FunctionEnd

Section "Talipot"
  SetShellVarContext all
  SetOutPath $INSTDIR
  SetRegView 64
  Call CheckAndInstallPython

  File /r files\*.*

  CreateDirectory "$SMPROGRAMS\Talipot-@TalipotVersion@ (x64)"
  CreateShortCut "$SMPROGRAMS\Talipot-@TalipotVersion@ (x64)\Talipot-@TalipotVersion@ (x64).lnk" "$INSTDIR\bin\talipot.bat" "" "$INSTDIR\share\talipot\bitmaps\talipot.ico"
  CreateShortCut "$SMPROGRAMS\Talipot-@TalipotVersion@ (x64)\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
  CreateShortCut "$DESKTOP\Talipot-@TalipotVersion@ (x64).lnk" "$INSTDIR\bin\talipot.bat" "" "$INSTDIR\share\talipot\bitmaps\talipot.ico"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Talipot-@TalipotVersion@" "DisplayName" "Talipot-@TalipotVersion@ (x64)"

  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Talipot-@TalipotVersion@" "UninstallString" "$INSTDIR\Uninstall.exe"
  WriteUninstaller "$INSTDIR\Uninstall.exe"

  ${registerExtension} "$INSTDIR\bin\talipot.bat" ".tlp" "Talipot.File" "Talipot Graph File" "$INSTDIR\share\talipot\bitmaps\talipot-file-icon.ico"
  ${registerExtension} "$INSTDIR\bin\talipot.bat" ".tlpb" "Talipot.BinaryFile" "Talipot Graph Binary File" "$INSTDIR\share\talipot\bitmaps\talipot-file-icon.ico"
  ${registerExtension} "$INSTDIR\bin\talipot.bat" ".tlpz" "Talipot.File.Gzip" "Talipot Graph File (gzipped)" "$INSTDIR\share\talipot\bitmaps\talipot-file-icon.ico"
  ${registerExtension} "$INSTDIR\bin\talipot.bat" ".tlpbz" "Talipot.BinaryFile.Gzip" "Talipot Graph Binary File (gzipped)" "$INSTDIR\share\talipot\bitmaps\talipot-file-icon.ico"
  ${registerExtension} "$INSTDIR\bin\talipot.bat" ".tlpx" "Talipot.ProjectFile" "Talipot Project File" "$INSTDIR\share\talipot\bitmaps\talipot-file-icon.ico"

  Call RefreshShellIcons

SectionEnd

Section "Uninstall"
  SetShellVarContext all
  SetRegView 64
  Delete "$DESKTOP\Talipot-@TalipotVersion@ (x64).lnk"
  RMDir /r "$SMPROGRAMS\Talipot-@TalipotVersion@ (x64)"
  RMDir /r "$INSTDIR"
  Delete $INSTDIR\Uninstall.exe
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Talipot-@TalipotVersion@"

  ${unregisterExtension} ".tlp" "Talipot.File"
  ${unregisterExtension} ".tlpb" "Talipot.BinaryFile"
  ${unregisterExtension} ".tlpz" "Talipot.File.Gzip"
  ${unregisterExtension} ".tlpbz" "Talipot.BinaryFile.Gzip"
  ${unregisterExtension} ".tlpx" "Talipot.ProjectFile"

  Call un.RefreshShellIcons

SectionEnd
